
func pull_interface() -> (
  pg_catalog::from_pg_namespace()
  | std::filter(func (n) -> (
    n.nspname != "pg_catalog"
    && n.nspname != "pg_toast"
    && n.nspname != "information_schema"
  ))
  | std::map(func (n: pg_catalog::PgNamespaceRow) -> {
    schema_name = n.nspname,
    tables = (
      pg_catalog::from_pg_class()
      | std::filter(func (t: pg_catalog::PgClass) -> (
        t.relnamespace == n.oid
        && t.relkind == "r"
      ))
      | std::map(func (t: pg_catalog::PgClass) -> {
        table_name = t.relname,
        columns = (
          pg_catalog::from_pg_attribute()
          | std::filter(func (a) -> a.attrelid == t.oid)
          | std::filter(func (a) -> a.attnum > 0 && a.atttypid > 0)
          | std::sort(func (a) -> (a.attnum | std::to_int64))
          | std::map(func (a) -> {
            name = a.attname,
            typ_id = (a.atttypid | std::to_int32),
            is_nullable = !a.attnotnull,
          })
        ),
        indexes = (
          pg_catalog::from_pg_index()
          | std::filter(func (i) -> i.indrelid == t.oid)
          | std::map(func (i) -> (
            pg_catalog::from_pg_class()
            | std::find(func (ci) -> ci.oid == i.indexrelid)
            | func (ci) -> {i = i, ci = ci}
          ))
          | std::map(func (this) -> {
            index_name = this.ci.relname,
            is_unique = this.i.indisunique,
            columns = (
              std::sql::unpack_array(this.i.indkey)
              | std::map(func (ik) -> (
                pg_catalog::from_pg_attribute()
                | std::find(func (a) ->
                  a.attrelid == this.i.indrelid && a.attnum == ik
                )
              ))
              | std::map(func (a) -> a.attname)
            ),
          })
        ),
      })
    )
  })
)

module pg_catalog {
  type PgTypeRow: {oid: int32, typname: text, typnamespace: int32, typowner: int32, typlen: int16, typbyval: bool, typtype: text, typcategory: text, typispreferred: bool, typisdefined: bool, typdelim: text, typrelid: int32, typsubscript: text, typelem: int32, typarray: int32, typinput: text, typoutput: text, typreceive: text, typsend: text, typmodin: text, typmodout: text, typanalyze: text, typalign: text, typstorage: text, typnotnull: bool, typbasetype: int32, typtypmod: int32, typndims: int32, typcollation: int32, typdefaultbin: text, typdefault: text, typacl: [text]}
  func from_pg_type(): [PgTypeRow] -> std::sql::from("pg_catalog/pg_type")

  type PgAttributeRow: {attrelid: int32, attname: text, atttypid: int32, attlen: int16, attnum: int16, attcacheoff: int32, atttypmod: int32, attndims: int16, attbyval: bool, attalign: text, attstorage: text, attcompression: text, attnotnull: bool, atthasdef: bool, atthasmissing: bool, attidentity: text, attgenerated: text, attisdropped: bool, attislocal: bool, attinhcount: int16, attstattarget: int16, attcollation: int32, attacl: [text], attoptions: [text], attfdwoptions: [text], attmissingval: text}
  func from_pg_attribute(): [PgAttributeRow] -> std::sql::from("pg_catalog/pg_attribute")

  type PgClass: {oid: int32, relname: text, relnamespace: int32, reltype: int32, reloftype: int32, relowner: int32, relam: int32, relfilenode: int32, reltablespace: int32, relpages: int32, reltuples: float32, relallvisible: int32, reltoastrelid: int32, relhasindex: bool, relisshared: bool, relpersistence: text, relkind: text, relnatts: int16, relchecks: int16, relhasrules: bool, relhastriggers: bool, relhassubclass: bool, relrowsecurity: bool, relforcerowsecurity: bool, relispopulated: bool, relreplident: text, relispartition: bool, relrewrite: int32, relfrozenxid: text, relminmxid: text, relacl: [text], reloptions: [text], relpartbound: text}
  func from_pg_class(): [PgClass] -> std::sql::from("pg_catalog/pg_class")

  type PgConstraintRow: {oid: int32, conname: text, connamespace: int32, contype: text, condeferrable: bool, condeferred: bool, convalidated: bool, conrelid: int32, contypid: int32, conindid: int32, conparentid: int32, confrelid: int32, confupdtype: text, confdeltype: text, confmatchtype: text, conislocal: bool, coninhcount: int16, connoinherit: bool, conkey: [int16], confkey: [int16], conpfeqop: [int32], conppeqop: [int32], conffeqop: [int32], confdelsetcols: [int16], conexclop: [int32], conbin: text}
  func from_pg_constraint(): [PgConstraintRow] -> std::sql::from("pg_catalog/pg_constraint")

  type PgIndexRow: {indexrelid: int32, indrelid: int32, indnatts: int16, indnkeyatts: int16, indisunique: bool, indnullsnotdistinct: bool, indisprimary: bool, indisexclusion: bool, indimmediate: bool, indisclustered: bool, indisvalid: bool, indcheckxmin: bool, indisready: bool, indislive: bool, indisreplident: bool, indkey: text, indcollation: text, indclass: text, indoption: text, indexprs: text, indpred: text}
  func from_pg_index(): [PgIndexRow] -> std::sql::from("pg_catalog/pg_index")

  type PgNamespaceRow: {oid: int32, nspname: text, nspowner: int32, nspacl: [text]}
  func from_pg_namespace(): [PgNamespaceRow] -> std::sql::from("pg_catalog/pg_namespace")
}

