# --- [ Complex tests ] ---
#
# This file contains tests for the language itself.
# It is read by the main.rs and exposed as a series of cargo-compatible tests.
# A test consists of:
#
#    #  === <test name>       (test name)
#    # skip: interpreter       (optional)
#    # skip: postgres          (optional)
#    <lutra project code>
#    # <                   (input marker)
#    <input>                   (optional)
#    # >                  (output marker)
#    <output>

# === chinook_00
module chinook {
  import std::filter
  import std::index
  import std::option::or_default

  type Album: {id: int64, title: text}

  func get_albums(): [Album] -> [
    {id = 3, title = "Hello world!"}, {id = 5, title = "Foo"}
  ]

  func get_album_by_id(album_id: int64): Album -> (
    get_albums()
    | filter(func (this: Album) -> this.id == album_id)
    | index(0)
    | or_default()
  )
}
module box_office {
  import std::filter
  import std::index
  import std::option::or_default

  type Invoice: {id: int64, total: float64}

  func get_album_sales(): [Invoice] -> [
    {id = 3, total = 3.6}, {id = 3, total = 3.1}, {id = 5, total = 5.2}
  ]

  func get_album_sales_by_id(album_id: int64): Invoice -> (
    get_album_sales()
    | filter(func (this: Invoice) -> this.id == album_id)
    | index(0)
    | or_default()
  )
}

func main() -> {
  chinook::get_albums(),
  chinook::get_album_by_id(3),
  box_office::get_album_sales_by_id(3),
}

# >
{
  [
    {
      id = 3,
      title = "Hello world!",
    },
    {
      id = 5,
      title = "Foo",
    },
  ],
  {
    id = 3,
    title = "Hello world!",
  },
  {
    id = 3,
    total = 3.6,
  },
}


# === chinook_01
module chinook {
  type Album: {id: int64, title: text}

  func get_album(): Album -> (
    {id = 3, title = "Hello world!"}
  )
}

func main() -> {
  5: int16,
  chinook::get_album()
}

# >
{
  5,
  {
    id = 3,
    title = "Hello world!",
  },
}


# === chinook_02
module chinook {
  type Album: {id: int64, title: text}

  func get_albums(): [Album] -> [
    {id = 3, title = "Hello world!"},
    {id = 5, title = "Foo"},
  ]

  func get_album_by_id(album_id: int64): Album -> (
    get_albums()
    | std::filter(func (this: Album) -> this.id == album_id)
    | std::index(0)
    | std::option::or_default()
  )
}

func main() -> {chinook::get_album_by_id(3)}

# >
{
  {
    id = 3,
    title = "Hello world!",
  },
}


# === chinook_03
module chinook {
  type Album: {title: text, genre_id: int64}
  type Genre: {id: int64, name: text}

  func get_genres(): [Genre] -> [
    {id = 1, name = "Rock"},
    {id = 2, name = "EDM"},
  ]

  func get_albums(): [Album] -> [
    {title = "Suck It and See", genre_id = 1},
    {title = "Random Access Memories", genre_id = 2},
    {title = "AM", genre_id = 1},
  ]

  func get_albums_by_genre(genre_id: int64): [Album] -> (
    get_albums()
    | std::filter(func (this: Album) -> this.genre_id == genre_id)
  )
}

import chinook as c

func main() -> (
  c::get_genres()
  | std::map(func (this: c::Genre) -> {
    name = this.name,
    albums = (
      c::get_albums_by_genre(this.id)
      | std::map(func (this: c::Album) -> this.title)
    ),
  })
)
# >
[
  {
    name = "Rock",
    albums = [
      "Suck It and See",
      "AM",
    ],
  },
  {
    name = "EDM",
    albums = [
      "Random Access Memories",
    ],
  },
]

# === compounding_interest_00

type Accumulator: {time: int32, repayment: float64, debt: float64}

const payments: [{time: int32, amount: float64}] = [
  {3, 10_000.0}, # no fmt
  {5, 10_000.0},
  {7, 10_000.0},
  {9, 11_200.0},
  {13, 10_000.0},
]

func main() -> (
  payments
  | std::fold(
    {time = 0, repayment = 0.0, debt = 50000.0},
    func (acc: Accumulator, payment: {time: int32, amount: float64}) -> (
      {
        time = payment.time,
        repayment = payment.amount,
        debt = (
          acc.debt
          * std::math::pow(1.0 + 0.05 / 12.0, payment.time - acc.time | std::to_float64)
          - payment.amount
        ),
      }
    ),
  )
)
# >
{
  time = 13,
  repayment = 10000.0,
  debt = 373.8299254002286,
}


# === compounding_interest_01

type Payment: {time: int32, amount: float64}

type State: {
  time: int32, debt: float64, last_payment: Payment,
}

const payments: [Payment] = [
  {3, 10000.0}, # no fmt
  {5, 10000.0},
  {7, 10000.0},
  {9, 11200.0},
  {13, 10373.0},
]

func apply_time(state: State, time: int32): State -> {
  time = time,
  debt = (
    state.debt
    * std::math::pow(1.0 + 0.05 / 12.0, time - state.time | std::to_float64)
  ),
  last_payment = state.last_payment,
}

func apply_payment(state: State, payment: Payment): State -> {
  time = state.time, debt = state.debt - payment.amount, last_payment = payment,
}

func simulate_debt(at_time: int32) -> (
  payments
  | std::filter(func (p: Payment) -> p.time <= at_time)
  | std::fold(
    {time = 0, debt = 50000.0, last_payment = std::default()}: State,
    func (state: State, payment: Payment) -> (
      state | apply_time(payment.time) | apply_payment(payment)
    ),
  )
  | apply_time(at_time)
)

func main() -> [10, 15] | std::map(x -> simulate_debt(x))

# >
[
  {
    time = 10,
    debt = 10245.230201028651,
    last_payment = {
      time = 9,
      amount = 11200.0,
    },
  },
  {
    time = 15,
    debt = 0.8368558536575957,
    last_payment = {
      time = 13,
      amount = 10373.0,
    },
  },
]

# === compounding_interest_02

import std::(Date, math::pow)

type Payment: {date: Date, amount: float64}

const payments: [Payment] = [
  {@2025-03-01, 10000.0},
  {@2025-05-01, 10000.0},
  {@2025-07-01, 10000.0},
  {@2025-09-01, 11200.0},
  {@2025-12-01, 10000.0},
]

type State: {date: Date, debt: float64}

const initial_state: State = {date = @2025-01-01, debt = 50000.0}

func main() -> std::fold(
  payments,
  initial_state,
  func (acc: State, payment: {date: Date, amount: float64}) -> {
    date = payment.date,
    debt = (
      acc.debt
      * pow(
        # 1 + monthly rate
        1.0 + 0.05 / 12.0,

        # duration in months
        payment.date.0 - acc.date.0
        | std::to_float64
        | std::div(30.0)
      )
      - payment.amount
    ),
  },
)

# >
{
  date = @2025-12-01,
  debt = 123.00277292101055,
}

# === advent_of_sql_2024_08
import std::(flat_map, filter, apply_until_empty)

func main() -> (from_staff() | apply_until_empty(get_managers))

type Iter: {id: int32, depth: int16}

func get_managers(level: [Iter]): [Iter] -> (
  level | flat_map(i -> get_staff_by_id(i.id))
)

func get_staff_by_id(staff_id: int32): [Iter] -> (
  from_staff() | filter(x -> x.id == staff_id)
)

func from_staff(): [Iter] -> []

# >
[]
