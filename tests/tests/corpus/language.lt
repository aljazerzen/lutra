# --- [ Language tests ] ---
#
# This file contains tests for the language itself.
# It is read by the main.rs and exposed as a series of cargo-compatible tests.
# A test consists of:
#
#    #  === <test name>       (test name)
#    # skip: interpreter       (optional)
#    # skip: postgres          (optional)
#    <lutra project code>
#    # <                   (input marker)
#    <input>                   (optional)
#    # >                  (output marker)
#    <output>

# === constant_00
const main = {1: int32, [false, true], "hello"}
# >
{
  1,
  [
    false,
    true,
  ],
  "hello",
}

# === bindings_00
const a: int32 = 4
func main() -> [{a, a + 1}, {a + 2, a + 3}]
# >
[
  {
    a = 4,
    5,
  },
  {
    a = 6,
    7,
  },
]

# === bindings_01
const a = {3: int32, false}
func main() -> [{a.0, a.1}, {a.0 + 1, !a.1}]
# >
[
  {
    3,
    false,
  },
  {
    4,
    true,
  },
]

# === bindings_02
const a: [int16] = [1, 2]
func main() -> [
  {
    a | std::index(0) | std::or_default,
    a | std::index(1) | std::or_default,
  },
  {
    a | std::index(0) | std::or_default | std::add(1),
    a | std::index(1) | std::or_default | std::add(1),
  }
]
# >
[
  {
    1,
    2,
  },
  {
    2,
    3,
  },
]

# === bindings_03
const a: [int16] = [1, 2, 3]
func main() -> {true, std::map(a, func (x: int16) -> x * 2), false}
# >
{
  true,
  [
    2,
    4,
    6,
  ],
  false,
}

# === bindings_04
const a: [int32] = [1, 2, 3]
func main() -> {
  true,
  a | std::map(func (x: int32) -> x * 2) | std::index(1),
  false
}
# >
{
  true,
  Some(4),
  false,
}

# === complex_00

const x: [int16] = [
  1, 4, 2, 3, 2, 3, 4, 5, 1, 2
]

func main() -> {
  a = 1: int32 + 2,
  {3: int64, 3 + 1: int16},
  {2: int32, 2 + 1: int16},
  hello = (
    x
    | std::map(func (y: int16) -> y + 1)
    | std::filter(func (y: int16) -> !(y > 3))
    | std::sort(func (x: int16) -> x)
    | std::map(func (y: int16) -> y % 3)
  )
}

# >
{
  a = 3,
  {
    3,
    4,
  },
  {
    2,
    3,
  },
  hello = [
    2,
    2,
    0,
    0,
    0,
  ],
}

# === default_00
func main(): bool -> std::default()
# >
false

# === default_01
func main(): int32 -> std::default()
# >
0

# === default_02
func main(): float32 -> std::default()
# >
0.0

# === default_03
func main(): text -> std::default()
# >
""

# === default_04
func main(): [int64] -> std::default()
# >
[]

# === default_05
func main(): {int8, text, [bool]} -> std::default()
# >
{
  0,
  "",
  [],
}

# === default_06
type Status: enum {
  Open: int16,
  Closed: bool,
}
func main(): Status -> std::default()
# >
Open(0)

# === default_10
func main(): [bool] -> std::lag([std::default(), std::default()], 1)
# >
[
  false,
  false,
]

# === default_11
func main(): [int32] -> std::lag([std::default(), std::default()], 1)
# >
[
  0,
  0,
]

# === default_12
func main(): [float32] -> std::lag([std::default(), std::default()], 1)
# >
[
  0.0,
  0.0,
]

# === default_13
func main(): [text] -> std::lag([std::default(), std::default()], 1)
# >
[
  "",
  "",
]

# === default_14
# skip: postgres
func main(): [[int64]] -> std::lag([std::default(), std::default()], 1)
# >
[
  [],
  [],
]

# === default_15
# skip: postgres
func main(): [{int8, text, [bool]}] -> std::lag([std::default(), std::default()], 1)
# >
[
  {
    0,
    "",
    [],
  },
  {
    0,
    "",
    [],
  },
]

# === default_16
# skip: postgres

type Status: enum {
  Open: int64,
  Closed: bool,
}
func main(): [Status] -> std::lag([std::default(), std::default()], 1)
# >
[
  Open(0),
  Open(0),
]

# === empty_array_00
func main(): [int64] -> []
# >
[]

# === empty_array_01
func main(): [{a: int64, b: text}] -> []
# >
[]

# === empty_array_02
func ident(x: [bool]) -> x
func main() -> ident([])
# >
[]

# === enum_construction_00

type Status: enum {Done, Pending: int64, Cancelled: text}
const main: Status = .Done
# >
Done

# === enum_construction_01

type Status: enum {Done, Pending: int64, Cancelled: text}
const main: Status = .Pending(513)
# >
Pending(513)

# === enum_construction_02

type Status: enum {Done, Pending: int64, Cancelled: text}
const main: Status = .Cancelled("I don't like it")
# >
Cancelled("I don't like it")

# === enum_construction_03

type Color: enum {Red, Green: bool, Blue: bool}
const main: Color = .Red
# >
Red

# === enum_construction_04

type Color: enum {Red, Green: int16, Blue: bool}
const main: Color = .Green(12312)
# >
Green(12312)

# === enum_construction_05

type Color: enum {Red, Green: bool, Blue: bool}
const main: Color = .Blue(false)
# >
Blue(false)

# === enum_construction_06
type Item: {
  id: int64,
  color: enum {Red, Green: bool, Blue: bool},
}
const main: Item = {
  id = 4,
  color = .Green(true)
}
# >
{
  id = 4,
  color = Green(true),
}

# === enum_construction_08

type Animal: enum {
  Cat: text,
  Dog: enum {Collie: text, Generic},
}

func main(): [Animal] -> [
  .Cat("Whiskers"),
  .Dog(.Collie("Belie")),
  .Dog(.Generic),
]
# >
[
  Cat("Whiskers"),
  Dog(Collie("Belie")),
  Dog(Generic),
]

# === enum_construction_09

type Status: enum {
  Open: int16,
  Closed: bool,
}
func main(): [Status] -> [.Open(0), .Open(0)]
# >
[
  Open(0),
  Open(0),
]

# === enum_construction_10

func main(): [enum {None, Some: text}] -> [
  .None,
  .Some("Whiskers"),
]
# >
[
  None,
  Some("Whiskers"),
]

# === enum_construction_11

type Animal: enum {
  Cat: text,
  Dog: enum {None, Some: text},
}

func main() -> [
  .Cat("Whiskers"): Animal,
  .Dog(.Some("Belie")),
  .Dog(.None),
]
# >
[
  Cat("Whiskers"),
  Dog(Some("Belie")),
  Dog(None),
]


# === functions_00
func add_one(x: int64) -> x + 1
func main() -> {add_one(4), add_one(add_one(5))}
# >
{
  5,
  7,
}

# === functions_01

func main() -> (
  4
  | func (x: int64) -> x + 1
  | func (x: int64) -> x * 2
)
# >
10

# === functions_02
func add_one(x: int64) -> x + 1
func add_two(x: int64) -> add_one(add_one(x))
func my_func(x: int64) -> add_two(x) + add_two(x)
func main() -> my_func(1)
# >
6

# === functions_03
func main() -> (
  4: int32
  | x -> x + 1
  | x -> x * 2
)
# >
10

# === if_00
func bool_to_text(x: bool) -> if x then (
  "yes"
) else (
  "no"
)

func main() -> {bool_to_text(true), bool_to_text(false)}
# >
{
  "yes",
  "no",
}

# === if_01
const en = [{true, "yes"}, {false, "no"}]
const sl = [{true, "da"}, {false, "ne"}]
const lang = "sl"

func main() -> if lang == "en" then en else sl
# >
[
  {
    true,
    "da",
  },
  {
    false,
    "ne",
  },
]

# === match_00
type Status: enum {Done, Pending: int64, Cancelled: text}
func main() -> match .Pending(4): Status {
  .Pending => "pending",
  .Cancelled => "cancelled",
}

# >
"pending"

# === match_01
type Status: enum {done, pending: int32, cancelled: text}
func main(): int32 -> match .pending(4): Status {
  .done => 0,
  .pending(x) => x,
  .cancelled => 0,
}

# >
4

# === match_02
type Animal: enum {
  Cat: text,
  Dog: enum {Collie: text, Generic},
}
func main() -> (
  .Dog(.Collie("Belie"))
  | func (animal: Animal) -> match animal {
    .Cat(name) => f"Hello {name}",
    .Dog(.Generic) => "Who's a good boy?",
    .Dog(.Collie(name)) => f"Come here {name}",
  }
)

# >
"Come here Belie"

# === match_03

type Animal: enum {
  Cat: text,
  Dog: text,
}

func main() -> (
  [
    .Cat("Whiskers"),
    .Dog("Belie"),
  ]
  | std::map(func (animal: Animal) -> match animal {
    .Cat(name) => f"Hello {name}",
    .Dog(name) => "Who's a good boy?",
  })
)

# >
[
  "Hello Whiskers",
  "Who's a good boy?",
]


# === match_04
type Animal: enum {
  Cat: text,
  Dog: enum {Collie: text, Generic},
}
func main() -> (
  [
    .Cat("Whiskers"),
    .Dog(.Collie("Belie")),
    .Dog(.Generic),
  ]
  | std::map(func (animal: Animal) -> match animal {
    .Cat(name) => f"Hello {name}",
    .Dog(.Generic) => "Who's a good boy?",
    .Dog(.Collie(name)) => f"Come here {name}",
  })
)
# >
[
  "Hello Whiskers",
  "Come here Belie",
  "Who's a good boy?",
]

# === match_05
const name = "Tom"

func main() -> match name {
  "world" => "Hello world!",
  n => f"Hello, {n}!"
}
# >
"Hello, Tom!"

# === match_06
func main() -> (
  ["Tom", "world", "Alice"]
  | std::map(func (n) -> match n {
    "world" => "Hello world!",
    n => f"Hello, {n}!"
  })
)
# >
[
  "Hello, Tom!",
  "Hello world!",
  "Hello, Alice!",
]

# === match_07
type Animal: enum {
  Cat,
  Dog,
  Hamster,
}

func main() -> (
  [.Cat, .Dog, .Hamster, .Dog]: [Animal]
  | std::map(func (a) -> match a {
    .Cat | .Hamster => false,
    _ => true
  })
)
# >
[
  false,
  true,
  false,
  true,
]

# === match_08
type Animal: enum {
  Cat: text,
  Dog: enum {Collie: text, Generic},
}

func main(): [text] -> (
  [
    .Cat("Whiskers"),
    .Dog(.Generic),
    .Dog(.Collie("Jessie")),
  ]
  | std::map(func (a: Animal): text -> match a {
    .Cat(name) | .Dog(.Collie(name)) => name,
    _ => "<unnamed>"
  })
)
# >
[
  "Whiskers",
  "<unnamed>",
  "Jessie",
]

# === or_else_00
func main() -> .Some("hello") | std::or_else("world")
# >
"hello"

# === or_else_01
func main() -> .None | std::or_else("fallback")
# >
"fallback"

# === or_else_02
func main() -> .None: enum {None, Some: text} | std::or_default()
# >
""

# === std_to_text_00
func main() -> std::to_text(20: uint8)
# >
"20"

# === std_to_text_01
func main() -> std::to_text(-20: int8)
# >
"-20"

# === std_to_text_02
func main() -> std::to_text(1.10: float32)
# >
"1.1"

# === std_to_text_03
func main() -> std::to_text(-1121121221.0: float64)
# >
"-1121121221"

# === std_to_text_04
func main() -> std::to_text(false)
# >
"false"

# === std_add_float64_00
func main() -> (30.2 + 2.30): float64
# >
32.5

# === std_add_float64_01
func main() -> (2.30 + 30.2): float64
# >
32.5

# === std_add_float64_02
func main() -> (1.4 + 0.2): float64
# >
1.5999999999999999

# === std_add_int8_00
func main() -> (30 + 2): int8
# >
32

# === std_add_int8_01
func main() -> (-2 + 30): int8
# >
28

# === std_add_int16_00
func main() -> (30 + 2): int16
# >
32

# === std_add_int16_01
func main() -> (-2 + 30): int16
# >
28

# === std_add_int32_00
func main() -> (30 + 2): int32
# >
32

# === std_add_int32_01
func main() -> (-2 + 30): int32
# >
28

# === std_add_int64_00
func main() -> (30 + 2): int64
# >
32

# === std_add_int64_01
func main() -> (-2 + 30): int64
# >
28

# === std_add_uint8_00
func main() -> (30 + 2): uint8
# >
32

# === std_add_uint16_00
func main() -> (30 + 2): uint16
# >
32

# === std_add_uint16_01
# skip: postgres
func main() -> (16384 + 16384): uint16
# >
32768

# === std_add_uint32_00
func main() -> (30 + 2): uint32
# >
32

# === std_add_uint64_00
func main() -> (30 + 2): uint64
# >
32

# === std_aggregate_00
const my_rel: [{int64, int64}] = [{5,3},{65,1},{3, 2}]
func main() -> std::aggregate(my_rel, func (x: {[int64], [int64]}) -> {std::min(x.0), std::min(x.1)})
# >
{
  Some(3),
  Some(1),
}

# === std_aggregate_01
func main() -> std::aggregate(
  [{false,"hello"},{false,"world"},{true, "!"}],
  func (x: {[bool], [text]}) -> {std::index(x.0, 0), std::index(x.1, 2)})
# >
{
  Some(false),
  Some("!"),
}

# === std_all_00
func main() -> std::all([true, false, false, true])
# >
false

# === std_all_01
func main() -> std::all([false, false])
# >
false

# === std_all_02
func main() -> std::all([true, true, true])
# >
true

# === std_all_03
func main() -> std::all([])
# >
true

# === std_and
func main() -> {false && false, false && true, true && false, true && true}
# >
{
  false,
  false,
  false,
  true,
}

# === std_any_00
func main() -> std::any([true, false, false, true])
# >
true

# === std_any_01
func main() -> std::any([false, false])
# >
false

# === std_any_02
func main() -> std::any([true, true, true])
# >
true

# === std_any_03
func main() -> std::any([])
# >
false

# === std_append_00
func main(): [int64] -> std::append(
  [1, 2, 3],
  [4, 5],
)

# >
[
  1,
  2,
  3,
  4,
  5,
]

# === std_append_01
func main() -> std::append(
  ["He", "ll", "o"],
  ["Wo", "rld!"],
)
# >
[
  "He",
  "ll",
  "o",
  "Wo",
  "rld!",
]

# === std_fold_00
func main() -> std::fold(
  [1, 2, 3, 4, 5]: [int64],
  0,
  func (s: int64, n: int64): int64 -> n + s
)
# >
15

# === std_fold_01
func main() -> std::fold(
  [1, 2, 3, 4, 5]: [int64],
  {0, 0}: {sum: int64, count: int64},
  func (s, n: int64) -> {sum = s.sum + n, count = s.count + 1}
)
# >
{
  sum = 15,
  count = 5,
}

# === std_scan_00
func main() -> std::scan(
  [1, 2, 3, 4, 5]: [int64],
  0,
  func (s: int64, n: int64): int64 -> n + s
)
# >
[
  1,
  3,
  6,
  10,
  15,
]

# === std_scan_01
func main() -> std::scan(
  [1, 2, 3, 4, 5]: [int64],
  {0, 0}: {sum: int64, count: int64},
  func (s, n: int64) -> {sum = s.sum + n, count = s.count + 1}
)
# >
[
  {
    sum = 1,
    count = 1,
  },
  {
    sum = 3,
    count = 2,
  },
  {
    sum = 6,
    count = 3,
  },
  {
    sum = 10,
    count = 4,
  },
  {
    sum = 15,
    count = 5,
  },
]

# === std_apply_until_empty_00
func main() -> (
  [0: int16]
  | std::apply_until_empty(func (v) ->
    v
    | std::map(func (i) -> i + 1)
    | std::filter(func (i) -> i < 10)
  )
)
# >
[
  0,
  1,
  2,
  3,
  4,
  5,
  6,
  7,
  8,
  9,
]

# === std_sequence_00
func main() -> std::sequence(0: int32, 3)
# >
[
  0,
  1,
  2,
]

# === std_sequence_01
func main() -> std::sequence(10: int8, 12)
# >
[
  10,
  11,
]

# === std_sequence_02
func main() -> std::sequence(10: int32, 8)
# >
[]

# === std_sequence_03
func main() -> std::sequence(0: uint16, 3)
# >
[
  0,
  1,
  2,
]

# === std_sequence_04
func main() -> std::sequence(1000000000: int64, 1000000003)
# >
[
  1000000000,
  1000000001,
  1000000002,
]

# === std_sequence_05
func main() -> std::sequence(std::sum([1: int32, 2, 3]), 8)
# >
[
  6,
  7,
]

# === std_sequence_06
func main() -> (
  std::sequence(1: int32, 5)
  | std::scan(1, func (a, c) -> a * c)
)
# >
[
  1,
  2,
  6,
  24,
]

# === std_mean_00
func main() -> std::mean([]: [int64])
# >
NaN

# === std_mean_01
func main() -> std::mean([5,3,65,3,2,56,67: int64])
# >
28.714285714285715

# === std_mean_02
func main() -> std::mean([5,3,65,3,2,56,67: int32])
# >
28.714285714285715

# === std_mean_03
func main() -> std::mean([5,3,65,3,2,56,67: int16])
# >
28.714285714285715

# === std_mean_04
func main() -> std::mean([5,3,65,3,2: int8])
# >
15.6

# === std_mean_05
func main() -> std::mean([1.1, 1.3]: [float32])
# >
1.2000000476837158

# === std_mean_06
func main() -> std::mean([1.1, 1.3]: [float64])
# >
1.2000000000000002

# === std_mean_07
func main() -> std::mean([]: [float64])
# >
NaN

# === std_contains_00
func main() -> std::contains([5,3,65,3,2,56,67], 3: int16)
# >
true

# === std_contains_01
func main() -> std::contains([5,3,65,3,2,56,67], 7: int16)
# >
false

# === std_contains_02
func main() -> std::contains([], 2: int16)
# >
false

# === std_contains_03
func main() -> std::contains(["hello", "world"], "world")
# >
true

# === std_contains_04
func main() -> std::contains([false, false, false, false, false], true)
# >
false

# === std_count_00
func main() -> std::count([5: int16,3,65,3,2,56,67])
# >
7

# === std_count_01
func main() -> std::count([]: [bool])
# >
0

# === std_count_02
func main() -> std::count([.Some("a"), .None]: [enum {None, Some: text}])
# >
2

# === std_div_00
func main() -> (10 / 6): int64
# >
1

# === std_div_01
func main() -> (-10 / 6): int64
# >
-1

# === std_div_02
func main() -> (10 / -6): int64
# >
-1

# === std_div_03
func main() -> (-10 / -6): int64
# >
1

# === std_div_04
func main() -> (10.0 / 6.0): float64
# >
1.6666666666666667

# === std_div_05
func main() -> (-10.0 / 6.0): float64
# >
-1.6666666666666667

# === std_div_06
func main() -> (10.0 / -6.0): float64
# >
-1.6666666666666667

# === std_div_07
func main() -> (-10.0 / -6.0): float64
# >
1.6666666666666667


# === std_filter_00
func main() -> std::filter([5,3,65,3,2]: [int64], func (x: int64) -> x > 3)
# >
[
  5,
  65,
]

# === std_filter_01
func main() -> std::filter([5,3,65,3,2]: [int64], func (x: int64) -> x < 1)
# >
[]

# === std_filter_02
func main() -> std::filter([], func (x: int64) -> x > 3)
# >
[]

# === std_filter_03
func main() -> std::filter([false,true,true,false,true], func (x: bool) -> !x)
# >
[
  false,
  false,
]

# === std_filter_04
func main() -> std::filter(
  [{false, "one"},{true, "two"},{true, "three"},{false, "four"},{true, "five"}],
  func (x: {bool, text}) -> x.0
)
# >
[
  {
    true,
    "two",
  },
  {
    true,
    "three",
  },
  {
    true,
    "five",
  },
]


# === std_find_00
func main() -> std::find([5,3,65,11,2]: [int64], func (x) -> x > 10)
# >
Some(65)

# === std_find_01
func main() -> std::find([5,3,65,3,2]: [int64], func (x) -> x < 0)
# >
None


# === std_flat_map_00
func main() -> std::flat_map([1, 2, 3]: [int64], func (x) -> [x, x])
# >
[
  1,
  1,
  2,
  2,
  3,
  3,
]

# === std_flat_map_01
func main() -> (
  [{1: int64, false}, {2, true}]
  | std::flat_map(func (x) -> [x, x])
)
# >
[
  {
    1,
    false,
  },
  {
    1,
    false,
  },
  {
    2,
    true,
  },
  {
    2,
    true,
  },
]

# === std_from_columnar_00
func main() -> std::from_columnar({[4: int16, 3, 2], [5: int32, 4, 1]})
# >
[
  {
    4,
    5,
  },
  {
    3,
    4,
  },
  {
    2,
    1,
  },
]

# === std_from_columnar_01
func main() -> std::from_columnar({[1]: [int32], []: [int64]})
# >
[]

# === std_from_columnar_02
func main() -> std::from_columnar({[]: [int8], [2]: [int16]})
# >
[]

# === std_from_columnar_03
func main() -> std::from_columnar({[1: int16, 2], [3: int16, 4, 5]})
# >
[
  {
    1,
    3,
  },
  {
    2,
    4,
  },
]

# === std_from_columnar_04
func main() -> std::from_columnar({[false, true], ["no", "yes", "neither"]})
# >
[
  {
    false,
    "no",
  },
  {
    true,
    "yes",
  },
]

# === std_from_columnar_05
func main() -> std::from_columnar({[1: int16, 2]})
# >
[
  {
    1,
  },
  {
    2,
  },
]

# === std_from_columnar_06
func main() -> std::from_columnar({})
# >
[]

# === std_from_columnar_07
func main() -> std::from_columnar({[false, true], ["no", "yes", "neither"], [1: int16, 2]})
# >
[
  {
    false,
    "no",
    1,
  },
  {
    true,
    "yes",
    2,
  },
]

# === std_zip_00
func main() -> (
  [1, 2, 3, 4: int16]
  | std::zip(["one", "two", "three"])
)
# >
[
  {
    1,
    "one",
  },
  {
    2,
    "two",
  },
  {
    3,
    "three",
  },
]

# === std_group_00
func main() -> (
  std::group([1, 2, 5, 1, 2, 4, 6, 12]: [int64], func (x) -> x % 2)
  | std::sort(func (x) -> x.key)
)
# >
[
  {
    key = 0,
    values = [
      2,
      2,
      4,
      6,
      12,
    ],
  },
  {
    key = 1,
    values = [
      1,
      5,
      1,
    ],
  },
]

# === std_group_01
func main() -> (
  [
    {1, false, 10},
    {2, false, 21},
    {2, false, 22},
    {1, true, 10}
  ]: [{int64, bool, int64}]
  | std::group(func (x: {int64, bool, int64}) -> {x.0, x.2})
  | std::sort(func (x) -> x.key.0 + x.key.1)
)

# >
[
  {
    key = {
      1,
      10,
    },
    values = [
      {
        1,
        false,
        10,
      },
      {
        1,
        true,
        10,
      },
    ],
  },
  {
    key = {
      2,
      21,
    },
    values = [
      {
        2,
        false,
        21,
      },
    ],
  },
  {
    key = {
      2,
      22,
    },
    values = [
      {
        2,
        false,
        22,
      },
    ],
  },
]

# === std_group_02
func main() -> (
  [
    {1, false, 10},
    {2, false, 21},
    {2, false, 22},
    {1, true, 10}
  ]: [{int64, bool, int64}]
  | std::group(
    func (x: {int64, bool, int64}) -> {x.0, x.2}
  )
  | std::sort(func (x) -> x.key.0 + x.key.1)
  | std::map(func (partition) -> {
    k = partition.key.0,
    v = std::sum(std::map(
      partition.values,
      func (x: {int64, bool, int64}) -> x.2
    )),
  })
)
# >
[
  {
    k = 1,
    v = 20,
  },
  {
    k = 2,
    v = 21,
  },
  {
    k = 2,
    v = 22,
  },
]

# === std_group_map_00
func main() -> (
  [1, 2, 2, 2, 5: int64, 5]
  | std::group_map(
    func (x) -> x, func (key, values) -> {key, std::count(values)}
  )
  | std::sort(func (x) -> x.key)
)
# >
[
  {
    key = 1,
    1,
  },
  {
    key = 2,
    3,
  },
  {
    key = 5,
    2,
  },
]

# === std_distinct_00
func main() -> (
  [1, 2, 2, 2, 5: int64, 5]
  | std::distinct()
  | std::sort(func (x) -> x)
)
# >
[
  1,
  2,
  5,
]

# === std_cmp_00
import std::cmp

const a: int32 = 2
const b: int32 = 30

func main() -> {cmp(a, b), cmp(a, a), cmp(b, a)}
# >
{
  Less,
  Equal,
  Greater,
}

# === std_eq_00
func main() -> {30: int64 == 2, 30: int64 == 30}
# >
{
  false,
  true,
}

# === std_eq_01
func main() -> {30.3: float64 == 2.2, 30.3: float64 == 30.3}
# >
{
  false,
  true,
}

# === std_eq_02
func main() -> {false == true, false == false}
# >
{
  false,
  true,
}

# === std_eq_03
func main() -> {"aa" == "b", "aa" == "aa"}
# >
{
  false,
  true,
}


# === std_ne_00
func main() -> {
  30.3: float64 != 2.2,
  30.3: float64 != 30.3
}
# >
{
  true,
  false,
}

# === std_ne_01
func main() -> {
  30.3: float64 != 2.2,
  30.3: float64 != 30.3
}
# >
{
  true,
  false,
}

# === std_ne_02
func main() -> {false != true, false != false}
# >
{
  true,
  false,
}

# === std_ne_03
func main() -> {"aa" != "b", "aa" != "aa"}
# >
{
  true,
  false,
}

# === std_lt_00
func main() -> {3 < 2: int64, 2 < 3: int64, 2 < 2: int64}
# >
{
  false,
  true,
  false,
}

# === std_lt_01
func main() -> {3.3 < 2.2: float64, 2.2 < 3.3: float64, 2.2 < 2.2: float64}
# >
{
  false,
  true,
  false,
}

# === std_lt_02
func main() -> {false < false, false < true, true < false, true < true}
# >
{
  false,
  true,
  false,
  false,
}

# === std_lt_03
func main() -> {"aa" < "aa", "aa" < "ab", "ab" < "aa", "ab" < "ab"}
# >
{
  false,
  true,
  false,
  false,
}


# === std_lte_00
func main() -> {3 <= 2: int64, 2 <= 3: int64, 2 <= 2: int64}
# >
{
  false,
  true,
  true,
}

# === std_lte_01
func main() -> {3.3 <= 2.2: float64, 2.2 <= 3.3: float64, 2.2 <= 2.2: float64}
# >
{
  false,
  true,
  true,
}

# === std_lte_02
func main() -> {false <= false, false <= true, true <= false, true <= true}
# >
{
  true,
  true,
  false,
  true,
}

# === std_lte_03
func main() -> {"aa" <= "aa", "aa" <= "ab", "ab" <= "aa", "ab" <= "ab"}
# >
{
  true,
  true,
  false,
  true,
}

# === std_gt_00
func main() -> {3 > 2: int64, 2 > 3: int64, 2 > 2: int64}
# >
{
  true,
  false,
  false,
}

# === std_gt_01
func main() -> {3.3 > 2.2: float64, 2.2 > 3.3: float64, 2.2 > 2.2: float64}
# >
{
  true,
  false,
  false,
}

# === std_gt_02
func main() -> {false > false, false > true, true > false, true > true}
# >
{
  false,
  false,
  true,
  false,
}

# === std_gt_03
func main() -> {"aa" > "aa", "aa" > "ab", "ab" > "aa", "ab" > "ab"}
# >
{
  false,
  false,
  true,
  false,
}

# === std_gte_00
func main() -> {3 >= 2: int64, 2 >= 3: int64, 2 >= 2: int64}
# >
{
  true,
  false,
  true,
}

# === std_gte_01
func main() -> {3.3 >= 2.2: float64, 2.2 >= 3.3: float64, 2.2 >= 2.2: float64}
# >
{
  true,
  false,
  true,
}

# === std_gte_02
func main() -> {false >= false, false >= true, true >= false, true >= true}
# >
{
  true,
  false,
  true,
  true,
}

# === std_gte_03
func main() -> {"aa" >= "aa", "aa" >= "ab", "ab" >= "aa", "ab" >= "ab"}
# >
{
  true,
  false,
  true,
  true,
}

# === std_index_00
func main() -> std::index([5,3,65,3,2]: [int64], 3)
# >
Some(3)

# === std_index_01
func main() -> std::index([5,3,65,3,2]: [int64], 10)
# >
None

# === std_index_02
func main() -> std::index([5.3,3.2,65.4,3.1,2.0], 3): enum { None, Some: float64 }
# >
Some(3.1)

# === std_index_04
func main() -> std::index([false, false, false, true, false], 3)
# >
Some(true)

# === std_index_06
func main() -> std::index(["hello", "world", "!"], 2)
# >
Some("!")

# === std_lag_00
func main() -> std::lag([5,3,65,3,2,56,67]: [int8], 2)
# >
[
  0,
  0,
  5,
  3,
  65,
  3,
  2,
]

# === std_lag_01
func main() -> std::lag([5,3,65,3,2,56,67]: [int16], 12)
# >
[
  0,
  0,
  0,
  0,
  0,
  0,
  0,
]

# === std_lag_02
func main(): [int64] -> std::lag([], 3)
# >
[]

# === std_lag_03
func main() -> std::lag([5,3,65,4], -2): [int8]
# >
[
  65,
  4,
  0,
  0,
]

# === std_lead_00
func main() -> std::lead([5,3,65,3,2,56,67], 2): [int8]
# >
[
  65,
  3,
  2,
  56,
  67,
  0,
  0,
]

# === std_lead_01
func main() -> std::lead([5,3,65,3,2,56,67], 12): [int64]
# >
[
  0,
  0,
  0,
  0,
  0,
  0,
  0,
]

# === std_lead_02
func main(): [int32] -> std::lead([], 3)
# >
[]

# === std_lead_03
func main() -> std::lead([5,3,65,4], -2): [int64]
# >
[
  0,
  0,
  5,
  3,
]

# === std_rolling_mean_00
func main() -> [1, 2, 3, 4]: [int32] | std::rolling_mean(1, 1)
# >
[
  1.5,
  2.0,
  3.0,
  3.5,
]

# === std_rolling_mean_01
func main() -> [1, 2, 3, 4]: [int8] | std::rolling_mean(1, 1)
# >
[
  1.5,
  2.0,
  3.0,
  3.5,
]

# === std_rolling_mean_02
func main() -> [1, 2, 3, 4]: [int8] | std::rolling_mean(0, 2)
# >
[
  2.0,
  3.0,
  3.5,
  4.0,
]

# === std_rolling_mean_03
func main() -> [1, 2, 3, 4]: [int8] | std::rolling_mean(2, 0)
# >
[
  1.0,
  1.5,
  2.0,
  3.0,
]

# === std_rolling_mean_04
func main() -> [1, 2, 3, 4]: [int8] | std::rolling_mean(100, 100)
# >
[
  2.5,
  2.5,
  2.5,
  2.5,
]

# === std_rolling_mean_05
func main() -> []: [int8] | std::rolling_mean(1, 1)
# >
[]

# === std_rank_00
func main() -> ["a", "b", "b", "c"] | std::rank()
# >
[
  1,
  2,
  2,
  4,
]

# === std_rank_01
func main() -> [3.0, 1.0, 1.0, 2.0]: [float32] | std::rank()
# >
[
  4,
  1,
  1,
  3,
]

# === std_rank_02
func main() -> []: [float32] | std::rank()
# >
[]

# === std_rank_dense_00
func main() -> ["a", "b", "b", "c"] | std::rank_dense()
# >
[
  1,
  2,
  2,
  3,
]

# === std_rank_percentile_00
func main() -> ["a", "b", "b", "c"] | std::rank_percentile()
# >
[
  0.0,
  0.3333333333333333,
  0.3333333333333333,
  1.0,
]

# === std_cume_dist_00
func main() -> ["a", "b", "b", "c"] | std::cume_dist()
# >
[
  0.25,
  0.75,
  0.75,
  1.0,
]

# === std_map_00
func main() -> std::map([5,3,65,3,2], func (x: int64) -> x + 1)
# >
[
  6,
  4,
  66,
  4,
  3,
]

# === std_map_01
func main() -> std::map([], func (x: int64) -> x + 1)
# >
[]

# === std_map_02
func main() -> std::map([false, true, false], func (x: bool) -> !x)
# >
[
  true,
  false,
  true,
]

# === std_map_03
func main() -> std::map(["hello", "world", "!"], func (x: text) -> std::text::length(x))
# >
[
  5,
  5,
  1,
]

# === std_map_columnar_00
func main() -> std::map_columnar(
  [{5: int16, 3: int16}, {65, 1}, {3, 2}],
  func (x: {[int16], [int16]}) -> {
    std::lag(x.0, 1),
    std::lead(x.1, 1)
  }
)

# >
[
  {
    0,
    1,
  },
  {
    5,
    2,
  },
  {
    65,
    0,
  },
]

# === std_map_columnar_01
func main() -> std::map_columnar(
    [{false,"hello"},{false,"world"},{true, "!"}],
    func (x: {[bool], [text]}) -> {
        std::lead(x.0, 1),
        std::lag(x.1, 1),
    }
)

# >
[
  {
    false,
    "",
  },
  {
    true,
    "hello",
  },
  {
    false,
    "world",
  },
]

# === std_max_00
func main() -> std::max([5,3,65,3,2,56,67]: [uint64])
# >
Some(67)

# === std_max_01
func main() -> std::max([]: [uint32])
# >
None

# === std_max_02
func main() -> std::max(["b", "d", "c"])
# >
Some("d")

# === std_max_03
func main() -> std::max([false, true, true, false])
# >
Some(true)

# === std_max_04
func main() -> std::max([]: [bool])
# >
None

# === std_max_05
func main() -> std::max([false, false])
# >
Some(false)

# === std_max_06
func main() -> std::max([true, true])
# >
Some(true)

# === std_min_00
func main() -> std::min([5,3,65,3,2,56,67]: [uint64])
# >
Some(2)

# === std_min_01
func main() -> std::min([]: [uint32])
# >
None

# === std_min_02
func main() -> std::min(["b", "d", "c"])
# >
Some("b")

# === std_min_03
func main() -> std::min([false, true, true, false])
# >
Some(false)

# === std_min_04
func main() -> std::min([]: [bool])
# >
None

# === std_min_05
func main() -> std::min([false, false])
# >
Some(false)

# === std_min_06
func main() -> std::min([true, true])
# >
Some(true)

# === std_mod_00
func main() -> (10 % 6): int64
# >
4

# === std_mod_01
func main() -> (-10 % 6): int64
# >
-4

# === std_mod_02
func main() -> (10 % -6): int64
# >
4

# === std_mod_03
func main() -> (-10 % -6): int64
# >
-4

# === std_mod_04
func main() -> (10.0 % 6.0): float64
# >
4.0

# === std_mod_05
func main() -> (-10.0 % 6.0): float64
# >
-4.0

# === std_mod_06
func main() -> (10.0 % -6.0): float64
# >
4.0

# === std_mod_07
func main() -> (-10.0 % -6.0): float64
# >
-4.0

# === std_mul_01
func main() -> (2 * 3): int64
# >
6

# === std_mul_02
func main() -> (2.1 * 3.5): float64
# >
7.3500000000000005

# === std_neg_00
func main() -> {-2: int64, - (-3: int64)}
# >
{
  -2,
  3,
}

# === std_neg_01
func main() -> {-2.1: float64, - (-3.1: float64)}
# >
{
  -2.1,
  3.1,
}

# === std_not
func main() -> {!false, !true}
# >
{
  true,
  false,
}

# === std_or
func main() -> {false || false, false || true, true || false, true || true}
# >
{
  false,
  true,
  true,
  true,
}

# === std_slice_00
func main() -> std::slice([5,3,65,3,2]: [int64], 1, 3)
# >
[
  3,
  65,
]

# === std_slice_01
# skip: postgres
func main() -> std::slice([5,3,65,3,2]: [int64], 1, -1)
# >
[
  3,
  65,
  3,
]

# === std_slice_02
func main() -> std::slice([5,3,65,3,2]: [int64], 4, 2)
# >
[]

# === std_slice_03
func main() -> std::slice([5,3,65,3,2]: [int64], 6, 7)
# >
[]

# === std_slice_04
func main() -> std::slice([5,3,65,3,2]: [int64], -7, 0)
# >
[]

# === std_slice_05
func main() -> std::slice([false,true,false,false,true], 1, 4)
# >
[
  true,
  false,
  false,
]

# === std_slice_06
func main() -> std::slice([{false,"hello"}, {false,"world"},{true, "!"},{false,"foo"},{true, "bar"}], 1, 4)
# >
[
  {
    false,
    "world",
  },
  {
    true,
    "!",
  },
  {
    false,
    "foo",
  },
]

# === std_sort_00
func main() -> std::sort([5,3,65,3,2]: [int64], func (x: int64) -> -x)
# >
[
  65,
  5,
  3,
  3,
  2,
]

# === std_sort_01
func main() -> std::sort(
  [{5: int64, "hello"}, {3: int64, "world"}, {65: int64, "!"}, {3: int64, "foo"}, {2: int64, "bar"}],
  func (x: {int64, text}) -> x.0
)
# >
[
  {
    2,
    "bar",
  },
  {
    3,
    "world",
  },
  {
    3,
    "foo",
  },
  {
    5,
    "hello",
  },
  {
    65,
    "!",
  },
]

# === std_sort_02
func main() -> std::sort(["ab", "a", "aa", "e"], func (x) -> x)
# >
[
  "a",
  "aa",
  "ab",
  "e",
]


# === std_sub_00
func main() -> (30 - 2): int64
# >
28

# === std_sub_01
func main() -> (2 - 30): int64
# >
-28

# === std_sub_02
func main() -> (30.2 - 2.30): float64
# >
27.9

# === std_sub_03
func main() -> (2.30 - 30.2): float64
# >
-27.9

# === std_sum_00
func main() -> std::sum([5,3,65,3,2,56,67: int32])
# >
201

# === std_sum_01
func main() -> std::sum([]: [int16])
# >
0

# === std_sum_02
func main() -> std::sum([5,3,65,3,2,56,67: int64])
# >
201

# === std_sum_03
func main() -> std::sum([5.4, 3.2, 1.1: float32])
# >
9.700001

# === std_to_columnar_00
func main() -> std::to_columnar([{5,3},{65,1},{3, 2}]: [{int64, int64}])
# >
{
  [
    5,
    65,
    3,
  ],
  [
    3,
    1,
    2,
  ],
}

# === std_to_columnar_01
func main() -> std::to_columnar([{false,"three"},{true,"one"},{false, "two"}])
# >
{
  [
    false,
    true,
    false,
  ],
  [
    "three",
    "one",
    "two",
  ],
}

# === std_to_columnar_02
func main() -> std::to_columnar([
  {false, "three", true},
  {true,  "one",   false},
  {false, "two",   false},
])
# >
{
  [
    false,
    true,
    false,
  ],
  [
    "three",
    "one",
    "two",
  ],
  [
    true,
    false,
    false,
  ],
}

# === std_to_columnar_03
func main() -> std::to_columnar([
  {},
  {},
  {},
])
# >
{}

# === std_text_length_00
func main() -> std::text::length("Hello world\n")
# >
12

# === std_text_length_01
func main() -> std::text::length("AljaÅ¾")
# >
5

# === std_text_from_ascii_00
func main() -> std::text::from_ascii(65)
# >
"A"

# === std_text_from_ascii_01
func main() -> std::text::from_ascii(10)
# >
"\n"

# === std_text_join_00
func main() -> std::text::join(["a", "b", "c"], ", ")
# >
"a, b, c"

# === std_text_join_01
func main() -> std::text::join(["a", "b", "c"], "")
# >
"abc"

# === std_text_join_02
func main() -> std::text::join([], ", ")
# >
""

# === std_text_split_00
func main() -> std::text::split("1_two_3", "_")
# >
[
  "1",
  "two",
  "3",
]

# === std_text_split_01
func main() -> std::text::split("1_two_3", "a")
# >
[
  "1_two_3",
]

# === std_text_starts_with_00
func main() -> {
  std::text::starts_with("bananas", "bana"),
  std::text::starts_with("bananas", "nana"),
}
# >
{
  true,
  false,
}

# === std_text_starts_with_01
const na = "na"

func main() -> std::text::starts_with("bananas", f"ba{na}")
# >
true

# === std_text_contains_00
func main() -> {
  std::text::contains("bananas", "nana"),
  std::text::contains("bananas", "apples"),
}
# >
{
  true,
  false,
}

# === std_text_ends_with_00
func main() -> {
  std::text::ends_with("bananas", "anas"),
  std::text::ends_with("bananas", "nana"),
}
# >
{
  true,
  false,
}

# === std_math_abs_00
func main() -> std::math::abs(-3: int16)
# >
3

# === std_math_abs_01
func main() -> std::math::abs(-3.3: float32)
# >
3.3

# === std_math_abs_02
func main() -> std::math::abs(0.0: float64)
# >
0.0

# === std_math_pi_00
func main(): float32 -> std::math::pi32
# >
3.1415927

# === std_math_pi_01
func main(): float64 -> std::math::pi64
# >
3.141592653589793

# === tuple_00
func main() -> {key = {"code 1", false}, value = 5: int64}.key.1
# >
false

# === unpack_00
const main = {a = 4: int32, ..{x1 = 3: int32, x2 = "hello"}, b = false}
# >
{
  a = 4,
  x1 = 3,
  x2 = "hello",
  b = false,
}

# === unpack_01
const x = {x1 = 3: int32, x2 = "hello"}
const main = {a = 4: int32, ..x, b = false}
# >
{
  a = 4,
  x1 = 3,
  x2 = "hello",
  b = false,
}

# === unpack_02
func identity(x: T): T
where T: {..}
-> x

func main() -> {a = 4: int32, ..identity({true, false}), b = false}
# >
{
  a = 4,
  true,
  false,
  b = false,
}

# === unpack_03
func false_x_false(x: T)
where T: {..}
-> {false, ..x, false}

func main() -> false_x_false({true, true})
# >
{
  false,
  true,
  true,
  false,
}

# === unpack_04
const main = {a = 4: int32, ..{x1 = 3: int32, x2 = "hello"}, b = false}.x2
# >
"hello"

# === unpack_05
const x = {x1 = 3: int32, x2 = "hello"}
const main = {a = 4: int32, ..x, b = false}.x2
# >
"hello"

# === unpack_06
func main() -> (
  {x1 = true, x2 = false}
  | func (x) -> {4: int32, ..x: {x1: bool, x2: bool}, "hello"}.x2
)
# >
false

# === unpack_07
func false_x_false(x: T) where T: {x2: text, ..} -> {false, ..x, false}.x2
func main() -> false_x_false({true, x2 = "hello"})
# >
"hello"

# === unpack_08
const main = {a = 4: int32, ..{x1 = 3: int32, x2 = "hello"}, b = false}.2
# >
"hello"

# === unpack_09
const main = {..{3: int32, "hello"}, ..{"world", ..{false, ..{true, true}}}, "a"}.6
# >
"a"

# === unpack_10
const x = {x1 = 3: int32, x2 = "hello"}
const main = {a = 4: int32, ..x, b = false}.2
# >
"hello"

# === unpack_12
func main() -> (
  {true, false}
  | func (x) -> {4: int32, ..x: {bool, bool}, "hello"}.2
)
# >
false

# === unpack_14
func false_x_false(x: T) where T: {bool, text, ..} -> {false, ..x, false}.2
func main() -> false_x_false({true, "hello"})
# >
"hello"


# === recursive_type_00
type Tree: enum {
  Leaf: int8,
  Node: {left: Tree, right: Tree},
}
const main: [Tree] = [
  .Leaf(10),
  .Leaf(12),
]
# >
[
  Leaf(10),
  Leaf(12),
]

# === recursive_type_01
type Tree: enum {
  Leaf: int8,
  Node: {left: Tree, right: Tree},
}
const main: Tree = .Node({
  left = .Leaf(10): Tree,
  right = .Node({
    left = .Node({
      left = .Leaf(12): Tree,
      right = .Leaf(18): Tree,
    }),
    right = .Leaf(20): Tree,
  }): Tree,
})
# >
Node({
  left = Leaf(10),
  right = Node({
    left = Node({
      left = Leaf(12),
      right = Leaf(18),
    }),
    right = Leaf(20),
  }),
})

# === recursive_type_02
type Tree: enum {
  Leaf: int8,
  Node: [Tree],
}
const main: Tree = .Node([
  .Leaf(10),
  .Node([
    .Node([
      .Leaf(12),
      .Leaf(15),
      .Leaf(18),
    ]),
    .Leaf(20),
  ]),
  .Leaf(21),
])
# >
Node([
  Leaf(10),
  Node([
    Node([
      Leaf(12),
      Leaf(15),
      Leaf(18),
    ]),
    Leaf(20),
  ]),
  Leaf(21),
])

# === date_00
const main = @2025-11-14
# >
@2025-11-14

# === date_01
const main: std::Date = @-10000-11-14
# >
@-10000-11-14

# === date_02
const main: std::Date = @100000-11-14
# >
@+100000-11-14

# === date_03
const main = std::Date(0)
# >
@1970-01-01

# === date_04
const main = @2025-11-14.0
# >
20406

# === date_05
const x = {a = @2025-11-15}
func main() -> x.a.0
# >
20407

# === time_00
const main = @16:07:44.123456
# >
@16:07:44.123456

# === time_01
const main = @16:07:44.12
# >
@16:07:44.120000

# === time_02
const main: std::Time = @213:06:00
# >
@213:06:00.000000

# === time_03
const main = @-5:12:31.12
# >
@-5:12:31.120000

# === time_04
const main = std::Time(0)
# >
@00:00:00.000000

# === time_05
func main() -> std::add_time(@116:07:44.12, @-4:00:00)
# >
@112:07:44.120000

# === std_date_to_timestamp_00
func main() -> std::date::to_timestamp(@2025-12-16, "UTC")
# >
@2025-12-16T00:00:00.000000

# === std_date_to_timestamp_01
func main() -> std::date::to_timestamp(@2025-12-16, "Europe/Ljubljana")
# >
@2025-12-15T23:00:00.000000

# === std_date_to_timestamp_02
func main() -> std::date::to_timestamp(@2025-12-16, "America/New_York")
# >
@2025-12-16T05:00:00.000000

# === std_date_to_year_month_day_00
func main() -> std::date::to_year_month_day(@2025-12-16)
# >
{
  year = 2025,
  month = 12,
  day = 16,
}

# === timestamp_00
const main = @2025-12-29T16:07:44.123456
# >
@2025-12-29T16:07:44.123456

# === timestamp_01
const main = @2025-12-29T16:07:44.12
# >
@2025-12-29T16:07:44.120000

# === var_binding_00
func main() -> (
  let a = false;
  {a, a, !a}
)
# >
{
  a = false,
  a = false,
  true,
}

# === var_binding_01
func main() -> (
  [1, 2, 3]: [int32]
  | std::map(func (x) -> (
    let a = x * 2;
    a * a
  ))
)
# >
[
  4,
  16,
  36,
]

# === var_binding_02
func main() -> (
  [1, 2, 3]: [int32]
  | std::map(func (x) -> (
    let a = x * 2;
    let b = a * 2;
    let c = -b;
    c
  ))
)
# >
[
  -4,
  -8,
  -12,
]
